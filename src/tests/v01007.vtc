varnishtest "VSLP director Rampup Time"

server s1 {
	rxreq
	txresp -body "ech3Ooj"
} -start

server s2 {
	rxreq
	txresp -body "ieQu2qua"
	rxreq
	txresp -body "ieQu2qua"
} -start

server s3 {
	rxreq
	txresp -body "xiuFi3Pe"
} -start

varnish v1 -vcl+backend {
	import vslp from "${vmod_topbuild}/src/.libs/libvmod_vslp.so" ;

	sub vcl_init {
		new vd = vslp.vslp();
		vd.add_backend(s1);
		vd.add_backend(s2);
		vd.add_backend(s3);
		vd.set_rampup_ratio(0);
		vd.set_rampup_time(15s);
		vd.init_hashcircle(25);
	}

	sub vcl_recv {
		set req.backend_hint = vd.backend_by_string("/eishoSu2");
		return(pass);
	}

	sub vcl_deliver {
		if(req.http.vrstart) {
			return(restart);
		}
	}

} -start

varnish v1 -cliok "backend.set_health s1 sick"

client c1 {
	txreq
	rxresp
	expect resp.body == "ieQu2qua"
} -run

varnish v1 -cliok "backend.set_health s1 healthy"

client c1 {
	txreq
	rxresp
	expect resp.body == "ieQu2qua"
} -run

delay 16

client c1 {
	txreq
	rxresp
	expect resp.body == "ech3Ooj"
} -run
