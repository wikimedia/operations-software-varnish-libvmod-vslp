varnishtest "VSLP director RS"

server s1 {
	rxreq
	txresp -body "ech3Ooj"
} -start

server s2 {
	rxreq
	txresp -body "ieQu2qua"
} -start

server s3 {
	rxreq
	txresp -body "xiuFi3Pe"
} -start

varnish v1 -vcl+backend {
	import vslp from "${vmod_topbuild}/src/.libs/libvmod_vslp.so" ;

	sub vcl_init {
		new vd = vslp.vslp();
		vd.add_backend(s1);
		vd.add_backend(s2);
		vd.add_backend(s3);
		vd.set_rampup_ratio(0);
		vd.set_hash(RS);
		vd.init_hashcircle(25);
	}

	sub vcl_recv {
		set req.backend_hint = vd.backend();
		return(pass);
	}

} -start


client c1 {
	txreq -url /we0eeTho
	rxresp
	expect resp.body == "ech3Ooj"

	txreq -url /mae8ooNu
	rxresp
	expect resp.body == "ieQu2qua"

	txreq -url /oob3dahS
	rxresp
	expect resp.body == "xiuFi3Pe"
} -run
